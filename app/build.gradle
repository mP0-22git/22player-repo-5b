plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

static def gitBranch() {
    def branch = 'GitHub'
    try {
        def gitcheck = 'command -v git >/dev/null 2>&1'.execute()
        gitcheck.waitFor()
        if (gitcheck.exitValue() == 0) {
            def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
            proc.in.eachLine { line -> branch = line }
            proc.err.eachLine { line -> println line }
            proc.waitFor()
        }
    } catch (Exception e) {
        // Do nothing
        println e
    }
    branch
}

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(Properties properties, String name, String defaultValue = "") {
    return properties?.getProperty(name) ?: defaultValue
}

// Load secrets from secrets.properties (gitignored)
def secretsFile = rootProject.file("secrets.properties")
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    namespace 'com.kabouzeid.trebl'
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35

        renderscriptTargetApi 30
        vectorDrawables.useSupportLibrary = true

        applicationId 'cyberwolf.musicplayer'
        versionCode 147
        versionName '3.0.10'

        // Inject secrets as BuildConfig fields
        buildConfigField "String", "GOOGLE_PLAY_LICENSE_KEY", "\"${secrets.getProperty('GOOGLE_PLAY_LICENSE_KEY', '')}\""
    }
    signingConfigs {
        release {
            def storeFilePath = secrets.getProperty('STORE_FILE', '')
            if (storeFilePath && file(storeFilePath).exists()) {
                storeFile file(storeFilePath)
                keyAlias secrets.getProperty('KEY_ALIAS', '')
                storePassword secrets.getProperty('STORE_PASSWORD', '')
                keyPassword secrets.getProperty('KEY_PASSWORD', '')
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (' + defaultConfig.versionCode + ') DEBUG (' + gitBranch() + ')'
        }
    }
    packaging {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
    lint {
        disable 'MissingTranslation', 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'

    implementation 'com.google.android.material:material:1.11.0'

    implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:6'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.github.afollestad:material-cab:0.1.12'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.android.billingclient:billing:7.1.1'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'

    //starrybg
    implementation 'com.github.sofakingforever:animated-stars-android:1.1.3'

    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'

    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'

    implementation 'com.github.AdrienPoupa:jaudiotagger:2.2.3'

    //google play ratings API (updated)
    implementation 'com.google.android.play:review:2.0.2'
    implementation 'com.google.android.play:app-update:2.1.0'

    implementation 'com.h6ah4i.android.widget.verticalseekbar:verticalseekbar:1.0.0'

    implementation 'me.tankery.lib:circularSeekBar:1.4.2'

    // AnimatedGradientTextView removed - repo no longer available
}
